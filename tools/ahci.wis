<WARPIN VERSION="1.0.15" OS="os2_45x">
 <HEAD>
  <TITLE>=("TITLE")</TITLE>
  <REXX NAME=TITLE>
   MyTitle="AHCI Driver for OS/2";

   BootDrive = SysBootDrive();
   ConfigSys = BootDrive||'\config.sys';
   OsDir=VALUE('OSDIR',,'OS2ENVIRONMENT');
   if (OsDir='') then OsDir='?:\OS2';
   OsDocTarget='TARGET="'||OsDir||'\DOC\AHCI" LONGFILENAMES SELECT';
   if (TRANSLATE(RIGHT(OsDir,3))='OS2') then do
     OsAppTarget='TARGET="'||OsDir||'\APPS" LONGFILENAMES SELECT';
   end
   else do
     OsAppTarget='TARGET="'||OsDir||'\BIN" LONGFILENAMES SELECT';
   end

   /* find AHCI line, if any */
   SearchString='BASEDEV=OS2AHCI.ADD';
   If (SysFileSearch(SearchString,ConfigSys,'test.') \= 0) then test.0 = 0;
   ahci_ln = 0;
   do i = 1 to test.0
     If (TRANSLATE(WORD(test.i, 1)) \= SearchString) then iterate;
     ahci_ln = i;
     leave;
   end

   /* find Dani line, if any */
   SearchString='BASEDEV=DANIS506.ADD';
   If (SysFileSearch(SearchString,ConfigSys,'test.') \= 0) then test.0 = 0;
   dani_ln = 0;
   do i = 1 to test.0
     If (TRANSLATE(WORD(test.i, 1)) \= SearchString) then iterate;
     dani_ln = i;
     leave;
   end

   ConfigSysModify = '';
   if (ahci_ln=0) then do
    if (dani_ln=0) then do
     ConfigSysModify='CONFIGSYS="BASEDEV=OS2AHCI.ADD | UNIQUE(OS2AHCI.ADD) ADDTOP"';
    end
    else do
     ConfigSysModify='CONFIGSYS="BASEDEV=OS2AHCI.ADD | UNIQUE(OS2AHCI.ADD) ADDBEFORE(DANIS506.ADD)"';
    end
   end

   rc = WirexxPutEnv('MyTitle',MyTitle);
   rc = WirexxPutEnv('ConfigSysModify',ConfigSysModify);
   rc = WirexxPutEnv('OsAppTarget',OsAppTarget);
   rc = WirexxPutEnv('OsDocTarget',OsDocTarget);

   Return MyTitle;
  </REXX>
  <REXX NAME=GetEnv>
   Parse Arg EnvVar .
   Return WirexxGetEnv(EnvVar);
  </REXX>

  <PCK INDEX=1 PACKAGEID="Arca Noae\AHCI\Driver\X\X\X"
   TITLE="AHCI Driver"
   TARGET="?:\OS2\BOOT" FIXED LONGFILENAMES SELECT
   =("GetEnv ConfigSysModify")
   >The AHCI Driver
  </PCK>

  <PCK INDEX=20 PACKAGEID="Arca Noae\AHCI\Documentation\X\X\X"
   TITLE="AHCI Documentation"
   =("GetEnv OsDocTarget")
   >ReadMe and License
  </PCK>
</HEAD>
 <BODY>
  <PAGE INDEX=1 TYPE=README>
   <TEXT>Welcome to the =("GetEnv MyTitle")</TEXT>
   <README EXTRACTFROMPCK="20" FORMAT="PLAIN">ReadMe.txt</README>
  </PAGE>
  <PAGE INDEX=2 TYPE=CONTAINER>
   <TEXT>Select the packages you want to install.
   </TEXT>
  </PAGE>
  <PAGE INDEX=3 TYPE=CONFIGURE>
    <TEXT>Press "Install" to begin installing this archive.</TEXT>
    <NEXTBUTTON TARGET=0>I~nstall</NEXTBUTTON>
  </PAGE>
 </BODY>
</WARPIN>
